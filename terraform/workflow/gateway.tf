# ---------------------------------------------------------------- AgentCore Gateway (MCP) + tools Lambda
# The chat runtime (agent/app.py) lists the tools through the gateway URL (SSM <prefix>/gateway-url) and calls them over MCP
# instead of its built-in functions. The Lambda runs the same agent/topology.py, agent/evidence.py and
# agent/proposals.py inside the VPC (subnet a), so it reads Neptune (terraform/pipeline/graph), the logs collection and the metrics
# workspace (terraform/pipeline/analytics). Proposals and the alert history are S3 Tables rows read through Athena
# (proposal_events / alert_events; Neptune holds no proposal since 2026-10-05).
# Without graph / analytics the topology comes from data/ and the evidence tools say so.

locals {
  tools = jsondecode(file("${path.module}/../../tools/tools.json"))

  # Lambda の zip に入れるファイル（プロジェクトの中の場所 = zip の中の名前）。
  # handler.py だけ名前が変わる（Lambda のハンドラが index.handler）。proposals.py は読むだけで、承認・却下はツールに出していない。
  # agent/ のモジュールを増やしたらここにも足す（同じ一覧が agent/Dockerfile と terraform/base/core の upload_web_command にもある）
  tools_files = {
    "../../tools/handler.py"         = "index.py"
    "../../agent/toolkit.py"         = "toolkit.py"
    "../../agent/topology.py"        = "topology.py"
    "../../agent/graph.py"           = "graph.py"
    "../../agent/evidence.py"        = "evidence.py"
    "../../agent/proposals.py"       = "proposals.py"
    "../../agent/data/topology.json" = "data/topology.json"
    "../../agent/data/layers.json"   = "data/layers.json"
  }
}

data "archive_file" "tools" {
  count = var.create_gateway ? 1 : 0

  type        = "zip"
  output_path = "${path.module}/.build/tools.zip"

  dynamic "source" {
    for_each = local.tools_files

    content {
      content  = file("${path.module}/${source.key}")
      filename = source.value
    }
  }

  # Lambda には PyYAML が無いので devices.yaml を JSON にして入れる（topology.load_static は devices.json を先に見る）
  source {
    content  = jsonencode(yamldecode(file("${path.module}/../../agent/data/devices.yaml")))
    filename = "data/devices.json"
  }
}

data "aws_iam_policy_document" "lambda_trust" {
  statement {
    actions = ["sts:AssumeRole"]

    principals {
      type        = "Service"
      identifiers = ["lambda.amazonaws.com"]
    }
  }
}

resource "aws_iam_role" "tools" {
  count = var.create_gateway ? 1 : 0

  name               = "${local.name_prefix}-tools"
  description        = "Tools Lambda behind the MCP gateway - reads Neptune (topology), the logs collection, the metrics workspace, the alert history and the proposals (Athena)"
  assume_role_policy = data.aws_iam_policy_document.lambda_trust.json
}

data "aws_iam_policy_document" "tools" {
  statement {
    sid       = "Logs"
    actions   = ["logs:CreateLogStream", "logs:PutLogEvents"]
    resources = ["arn:${local.partition}:logs:${var.region}:${local.account_id}:log-group:/aws/lambda/${local.name_prefix}-tools:*"]
  }

  # VPC の中で動くので ENI を作る（AWSLambdaVPCAccessExecutionRole と同じ中身。マネージドポリシーは付けない）
  statement {
    sid       = "VpcEni"
    actions   = ["ec2:CreateNetworkInterface", "ec2:DescribeNetworkInterfaces", "ec2:DeleteNetworkInterface", "ec2:AssignPrivateIpAddresses", "ec2:UnassignPrivateIpAddresses"]
    resources = ["*"]
  }

  # Neptune Analytics のグラフの ID（terraform/pipeline/graph）を SSM から引く
  statement {
    sid       = "Parameters"
    actions   = ["ssm:GetParameter"]
    resources = ["arn:${local.partition}:ssm:${var.region}:${local.account_id}:parameter${local.param_prefix}/*"]
  }

  # トポロジと status を読むだけ。Write は付けない（修復案は Neptune に無い。ツールに承認・却下も無い。proposals.tf の冒頭）
  dynamic "statement" {
    for_each = local.neptune_data_arn != "" ? [1] : []
    content {
      sid       = "NeptuneRead"
      actions   = ["neptune-graph:ReadDataViaQuery", "neptune-graph:GetQueryStatus"]
      resources = [local.neptune_data_arn]
    }
  }

  dynamic "statement" {
    for_each = local.opensearch_collection_arn != "" ? [1] : []
    content {
      sid       = "LogsCollection"
      actions   = ["aoss:APIAccessAll"]
      resources = [local.opensearch_collection_arn]
    }
  }

  dynamic "statement" {
    for_each = local.prometheus_workspace_arn != "" ? [1] : []
    content {
      sid       = "MetricsQuery"
      actions   = ["aps:QueryMetrics", "aps:GetSeries", "aps:GetLabels", "aps:GetMetricMetadata"]
      resources = [local.prometheus_workspace_arn]
    }
  }

  # query_history（アラートの通知の履歴）と list_proposals（修復案）。Athena で alert_events と proposal_events を読む 4 文
  # （locals.tf の history_read_statements。Web の EC2 にも同じものを付ける）。ワークグループが無ければ 1 文も無い
  dynamic "statement" {
    for_each = local.history_read_statements
    content {
      sid       = statement.value.sid
      actions   = statement.value.actions
      resources = statement.value.resources
    }
  }
}

# ---------------------------------------------------------------- tools Lambda network (subnet a of terraform/base/core)
# Lambda の SG は terraform/base/core の lambda（エンドポイントの 443 へ出られる。Neptune Analytics もそこ）。aoss / SSM / aps へは terraform/base/core の
# VPC エンドポイントを通る（ログは Lambda のサービスが書く）。
# 2026-09-26 まではここに tools の SG と 4 本のルールがあった（7c42b0f）

# 検索だけ。terraform/pipeline/analytics の data access policy は Spark の実行ロール（書く側）だけなので、読む側はここで足す
resource "aws_opensearchserverless_access_policy" "tools" {
  count = var.create_gateway && local.opensearch_collection_name != "" ? 1 : 0

  name        = "${local.name_prefix}-logs-read"
  type        = "data"
  description = "Tools Lambda and chat runtime read the logs collection"

  policy = jsonencode([{
    Rules = [
      {
        ResourceType = "collection"
        Resource     = ["collection/${local.opensearch_collection_name}"]
        Permission   = ["aoss:DescribeCollectionItems"]
      },
      {
        ResourceType = "index"
        Resource     = ["index/${local.opensearch_collection_name}/*"]
        Permission   = ["aoss:DescribeIndex", "aoss:ReadDocument"]
      },
    ]
    Principal = [aws_iam_role.tools[0].arn]
  }])
}

resource "aws_iam_role_policy" "tools" {
  count = var.create_gateway ? 1 : 0

  name   = "${local.name_prefix}-tools"
  role   = aws_iam_role.tools[0].name
  policy = data.aws_iam_policy_document.tools.json
}

# terraform/base/core の perimeter.tf の Deny。ログと ENI は Lambda のサービスがこのロールで出すので、Deny の対象に入っていない
resource "aws_iam_role_policy_attachment" "tools_perimeter" {
  count = var.create_gateway && local.perimeter_policy_arn != "" ? 1 : 0

  role       = aws_iam_role.tools[0].name
  policy_arn = local.perimeter_policy_arn
}

resource "aws_cloudwatch_log_group" "tools" {
  count = var.create_gateway ? 1 : 0

  name              = "/aws/lambda/${local.name_prefix}-tools"
  retention_in_days = var.log_retention_days
}

resource "aws_lambda_function" "tools" {
  count = var.create_gateway ? 1 : 0

  function_name    = "${local.name_prefix}-tools"
  role             = aws_iam_role.tools[0].arn
  runtime          = "python3.13"
  architectures    = ["arm64"]
  handler          = "index.handler"
  filename         = data.archive_file.tools[0].output_path
  source_code_hash = data.archive_file.tools[0].output_base64sha256
  timeout          = 60
  memory_size      = 256

  # VPC の中（var.lambda_az_num の AZ。既定はサブネット a だけ）。SG は terraform/base/core の lambda
  vpc_config {
    subnet_ids         = slice(local.subnet_ids, 0, var.lambda_az_num)
    security_group_ids = [local.lambda_sg_id]
  }

  environment {
    variables = {
      PARAM_PREFIX         = local.param_prefix # graph.py が <prefix>/neptune-graph-id を引く（無ければ data/ の静的トポロジ）
      OPENSEARCH_ENDPOINT  = local.opensearch_endpoint
      OPENSEARCH_INDEX     = local.opensearch_index
      PROMETHEUS_QUERY_URL = local.prometheus_query_url
      # query_history（evidence.py）と list_proposals（proposals.py）。どれかが空ならツールは「未配備」を返す
      ATHENA_WORKGROUP      = local.athena_workgroup
      ATHENA_CATALOG        = local.athena_catalog
      HISTORY_NAMESPACE     = local.athena_workgroup == "" ? "" : local.audit_namespace
      ALERT_EVENTS_TABLE    = local.alert_events_table_name
      PROPOSAL_EVENTS_TABLE = local.proposal_events_table_name
    }
  }

  depends_on = [aws_cloudwatch_log_group.tools, aws_iam_role_policy.tools]
}

# ---------------------------------------------------------------- gateway role (invokes the Lambda)
data "aws_iam_policy_document" "gateway_trust" {
  statement {
    actions = ["sts:AssumeRole"]

    principals {
      type        = "Service"
      identifiers = ["bedrock-agentcore.amazonaws.com"]
    }

    condition {
      test     = "StringEquals"
      variable = "aws:SourceAccount"
      values   = [local.account_id]
    }

    condition {
      test     = "ArnLike"
      variable = "aws:SourceArn"
      values   = ["arn:${local.partition}:bedrock-agentcore:${var.region}:${local.account_id}:gateway/${local.name_prefix}-tools-*"]
    }
  }
}

resource "aws_iam_role" "gateway" {
  count = var.create_gateway ? 1 : 0

  name               = "${local.name_prefix}-gateway"
  description        = "AgentCore Gateway role - invokes the tools Lambda"
  assume_role_policy = data.aws_iam_policy_document.gateway_trust.json
}

data "aws_iam_policy_document" "gateway" {
  statement {
    sid       = "InvokeToolsLambda"
    actions   = ["lambda:InvokeFunction"]
    resources = var.create_gateway ? [aws_lambda_function.tools[0].arn] : []
  }
}

resource "aws_iam_role_policy" "gateway" {
  count = var.create_gateway ? 1 : 0

  name   = "${local.name_prefix}-gateway"
  role   = aws_iam_role.gateway[0].name
  policy = data.aws_iam_policy_document.gateway.json
}

resource "aws_bedrockagentcore_gateway" "tools" {
  count = var.create_gateway ? 1 : 0

  name            = "${local.name_prefix}-tools"
  description     = "${local.name_prefix} agent tools (MCP)"
  role_arn        = aws_iam_role.gateway[0].arn
  authorizer_type = "AWS_IAM"
  protocol_type   = "MCP"

  protocol_configuration {
    mcp {
      supported_versions = ["2025-06-18"]
    }
  }

  depends_on = [aws_iam_role_policy.gateway]
}

# この VPC のエンドポイント（bedrock-agentcore.gateway）を通らない InvokeGateway を拒む（terraform/base/core の perimeter.tf の資源側。
# AgentCore の文書の DenyAllExceptVPC と同じ形）。呼ぶのはチャットの Runtime だけで、Runtime は VPC の中にいる
resource "aws_bedrockagentcore_resource_policy" "gateway" {
  count = var.create_gateway && local.perimeter_policy_arn != "" ? 1 : 0

  resource_arn = aws_bedrockagentcore_gateway.tools[0].gateway_arn
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Sid       = "DenyOutsideVpc"
      Effect    = "Deny"
      Principal = "*"
      Action    = "bedrock-agentcore:InvokeGateway"
      Resource  = aws_bedrockagentcore_gateway.tools[0].gateway_arn
      Condition = {
        StringNotEqualsIfExists = { "aws:SourceVpc" = local.vpc_id }
        BoolIfExists            = { "aws:ViaAWSService" = "false" }
        ArnNotLike              = { "aws:PrincipalArn" = local.perimeter_exempt_principals }
      }
    }]
  })
}

resource "aws_bedrockagentcore_gateway_target" "tools" {
  count = var.create_gateway ? 1 : 0

  name               = "tools"
  description        = "Read only tools backed by the tools Lambda (topology, proposals, logs, metrics)"
  gateway_identifier = aws_bedrockagentcore_gateway.tools[0].gateway_id

  credential_provider_configuration {
    gateway_iam_role {}
  }

  target_configuration {
    mcp {
      lambda {
        lambda_arn = aws_lambda_function.tools[0].arn

        tool_schema {
          dynamic "inline_payload" {
            for_each = local.tools
            content {
              name        = inline_payload.value.name
              description = inline_payload.value.description

              input_schema {
                type = inline_payload.value.inputSchema.type

                dynamic "property" {
                  for_each = inline_payload.value.inputSchema.properties
                  content {
                    name        = property.key
                    type        = property.value.type
                    description = try(property.value.description, null)
                    required    = contains(try(inline_payload.value.inputSchema.required, []), property.key)
                  }
                }
              }
            }
          }
        }
      }
    }
  }
}

# agent/app.py は URL を SSM から引く（環境変数 GATEWAY_URL でも上書きできる）
resource "aws_ssm_parameter" "gateway_url" {
  count = var.create_gateway ? 1 : 0

  name  = "${local.param_prefix}/gateway-url"
  type  = "String"
  value = aws_bedrockagentcore_gateway.tools[0].gateway_url
}
