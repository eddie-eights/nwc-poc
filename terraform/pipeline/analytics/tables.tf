# ---------------------------------------------------------------- S3 Tables (Iceberg)
# テーブルバケット名はハイフン可、namespace とテーブル名はアンダースコアだけ（S3 Tables の命名規則、2026-09-17 確認）。
# テーブルは Terraform で作る（terraform destroy でバケットまで消せるように。テーブルが残るとバケットは消えない）。
# 列は Telegraf の JSON（{"fields":{…},"name":"…","tags":{…},"timestamp":秒}）をそのまま持つ。
# tags と fields は JSON 文字列のまま入れる（機器やメトリクスが増えても列を変えないため。列の型は Iceberg のプリミティブだけ）
# テーブルバケットと namespace はいつも作る（証跡の proposal_events が入る。2026-09-24）。
# 生データの raw_telemetry だけは var.sinks に iceberg があるときだけ作る（deploy.env の SINK_S3。格納先は 1 つずつ 1 / 0 で選べる）
resource "aws_s3tables_table_bucket" "tables" {
  name = local.table_bucket
}

# この VPC のエンドポイントを通らない呼び出しを拒む（terraform/base/core の perimeter.tf の資源側）。
# Iceberg REST の中の呼び出しは元の VPC を引き継がないので aws:CalledViaLast = s3tables.amazonaws.com を外す。
# 表の保守（compaction など）は S3 Tables 自身が出すので aws:PrincipalIsAWSService で外れる。
# ポリシーの読み書きは外す（デプロイする人が変わって締め出されても、その人が delete-table-bucket-policy で戻せる）
resource "aws_s3tables_table_bucket_policy" "tables" {
  count = local.perimeter_policy_arn != "" ? 1 : 0

  table_bucket_arn = aws_s3tables_table_bucket.tables.arn
  resource_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Sid       = "DenyOutsideVpc"
      Effect    = "Deny"
      Principal = "*"
      NotAction = ["s3tables:GetTableBucketPolicy", "s3tables:PutTableBucketPolicy", "s3tables:DeleteTableBucketPolicy"]
      Resource  = [aws_s3tables_table_bucket.tables.arn, "${aws_s3tables_table_bucket.tables.arn}/*"]
      Condition = {
        StringNotEqualsIfExists = { "aws:SourceVpc" = local.vpc_id, "aws:CalledViaLast" = "s3tables.amazonaws.com" }
        BoolIfExists            = { "aws:ViaAWSService" = "false" }
        Bool                    = { "aws:PrincipalIsAWSService" = "false" }
        ArnNotLike              = { "aws:PrincipalArn" = local.perimeter_exempt_principals }
      }
    }]
  })
}

resource "aws_s3tables_namespace" "netops" {
  namespace        = var.namespace
  table_bucket_arn = aws_s3tables_table_bucket.tables.arn
}

# 2026-09-24 まではバケットと namespace も iceberg のときだけ（count）だった。state の [0] をそのまま引き継ぐ
moved {
  from = aws_s3tables_table_bucket.tables[0]
  to   = aws_s3tables_table_bucket.tables
}

moved {
  from = aws_s3tables_namespace.netops[0]
  to   = aws_s3tables_namespace.netops
}

# 2026-10-04 に snmp_metrics から改名した（metrics / gnmi / mdt / traps / logs の全トピックが入るので、SNMP のメトリクスだけに見えない名前に）。
# moved で state のアドレスを引き継ぐ。テーブルの名前（var.table_name）も変わるので、古いテーブルを残したまま apply すると
# 作り直し（中身は消える）になるかもしれない（AWS では未確認。この PoC はその日に消すので構わない）
moved {
  from = aws_s3tables_table.snmp_metrics
  to   = aws_s3tables_table.raw_telemetry
}

resource "aws_s3tables_table" "raw_telemetry" {
  count = local.sink_iceberg ? 1 : 0

  name             = var.table_name
  namespace        = aws_s3tables_namespace.netops.namespace
  table_bucket_arn = aws_s3tables_table_bucket.tables.arn
  format           = "ICEBERG"

  metadata {
    iceberg {
      schema {
        field {
          name     = "ts"
          type     = "timestamp"
          required = true
        }
        field {
          name     = "topic"
          type     = "string"
          required = true
        }
        field {
          name     = "measurement"
          type     = "string"
          required = false
        }
        field {
          name     = "agent_host"
          type     = "string"
          required = false
        }
        field {
          name     = "host"
          type     = "string"
          required = false
        }
        field {
          name     = "tags_json"
          type     = "string"
          required = false
        }
        field {
          name     = "fields_json"
          type     = "string"
          required = false
        }
        field {
          name     = "ingested_at"
          type     = "timestamp"
          required = true
        }
      }
    }
  }
}

# ---------------------------------------------------------------- 証跡（2026-09-24）
# 異常が開いた・閉じたの履歴（anomaly_events）は 2026-10-02 にやめた（書いていた Spark の detect をなくした。
# 障害の履歴をどこにどう残すかは決めていない。いまはアラートの履歴を Grafana / Splunk で見る）。
# 修復案の作成・承認・却下・適用・確認の履歴。書くのは terraform/workflow の worker（workflow/awsio.py の append_proposal_events、PyIceberg）。
# event は created / approved / rejected / expired / obsolete / applied / failed / verified。event_id = <proposal_id>#<event>。
# 列は workflow/rules.py の PROPOSAL_EVENT_COLUMNS と同じ順・同じ型。action / cause はエージェントの答え、decided_by は承認・却下した人。
# 修復案の「いま」は Neptune の proposal の頂点（Web の承認タブが読み書きする）で、ここは証跡
resource "aws_s3tables_table" "proposal_events" {
  name             = "proposal_events"
  namespace        = aws_s3tables_namespace.netops.namespace
  table_bucket_arn = aws_s3tables_table_bucket.tables.arn
  format           = "ICEBERG"

  metadata {
    iceberg {
      schema {
        field {
          name     = "event_id"
          type     = "string"
          required = false
        }
        field {
          name     = "proposal_id"
          type     = "string"
          required = false
        }
        field {
          name     = "anomaly_id"
          type     = "string"
          required = false
        }
        field {
          name     = "event"
          type     = "string"
          required = false
        }
        field {
          name     = "status"
          type     = "string"
          required = false
        }
        field {
          name     = "device_id"
          type     = "string"
          required = false
        }
        field {
          name     = "action"
          type     = "string"
          required = false
        }
        field {
          name     = "cause"
          type     = "string"
          required = false
        }
        field {
          name     = "command"
          type     = "string"
          required = false
        }
        field {
          name     = "decided_by"
          type     = "string"
          required = false
        }
        field {
          name     = "detail"
          type     = "string"
          required = false
        }
        field {
          name     = "event_time"
          type     = "timestamptz"
          required = false
        }
      }
    }
  }
}
