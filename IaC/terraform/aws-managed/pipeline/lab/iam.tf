resource "aws_iam_role" "lab" {
  name        = "${local.name_prefix}-lab"
  description = "${local.name_prefix} lab EC2 - SSM managed node, pull lab images from ECR, read lab/ from the asset bucket"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Effect    = "Allow"
      Principal = { Service = "ec2.amazonaws.com" }
      Action    = "sts:AssumeRole"
    }]
  })

  tags = { Name = "${local.name_prefix}-lab" }
}

resource "aws_iam_role_policy_attachment" "lab_ssm" {
  role       = aws_iam_role.lab.name
  policy_arn = "arn:${local.partition}:iam::aws:policy/AmazonSSMManagedInstanceCore"
}

resource "aws_iam_role_policy" "lab_assets" {
  name = "lab-assets"
  role = aws_iam_role.lab.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid      = "EcrPull"
        Effect   = "Allow"
        Action   = ["ecr:BatchGetImage", "ecr:GetDownloadUrlForLayer", "ecr:BatchCheckLayerAvailability"]
        Resource = "arn:${local.partition}:ecr:${var.region}:${local.account_id}:repository/${local.name_prefix}-lab-*"
      },
      {
        Sid      = "EcrToken"
        Effect   = "Allow"
        Action   = "ecr:GetAuthorizationToken"
        Resource = "*"
      },
      {
        Sid      = "S3Read"
        Effect   = "Allow"
        Action   = "s3:GetObject"
        Resource = "arn:${local.partition}:s3:::${local.bucket}/lab/*"
      },
      {
        Sid      = "S3List"
        Effect   = "Allow"
        Action   = "s3:ListBucket"
        Resource = "arn:${local.partition}:s3:::${local.bucket}"
        Condition = {
          StringLike = { "s3:prefix" = "lab/*" }
        }
      },
      {
        # lab.sh forward が Telegraf の NLB のアドレスとタスクのサブネットの CIDR を読む（terraform/pipeline/stream の telegraf.tf。無ければ何もしない）
        Sid    = "TelegrafAddress"
        Effect = "Allow"
        Action = "ssm:GetParameter"
        Resource = [
          "arn:${local.partition}:ssm:${var.region}:${local.account_id}:parameter/${local.name_prefix}/telegraf-address",
          "arn:${local.partition}:ssm:${var.region}:${local.account_id}:parameter/${local.name_prefix}/telegraf-source-cidr",
        ]
      },
    ]
  })
}

resource "aws_iam_instance_profile" "lab" {
  name = "${local.name_prefix}-lab"
  role = aws_iam_role.lab.name
}

# terraform/base/core の perimeter.tf の Deny（VPC エンドポイントを通らない呼び出しを拒む）
resource "aws_iam_role_policy_attachment" "lab_perimeter" {
  count = local.perimeter_policy_arn != "" ? 1 : 0

  role       = aws_iam_role.lab.name
  policy_arn = local.perimeter_policy_arn
}
